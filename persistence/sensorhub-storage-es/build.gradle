description = 'Elastic search Storage'
ext.details = 'Storage based on Elastic search database'
version = '1.0.0-SNAPSHOT'

dependencies {
  implementation 'org.sensorhub:sensorhub-core:' + oshCoreVersion
  implementation project(':sensorhub-storage-compat')
  embeddedImpl 'org.elasticsearch.client:transport:5.3.1'
  embeddedImpl 'com.esotericsoftware:kryo:4.0.0'
  embeddedImpl 'org.locationtech.spatial4j:spatial4j:0.6'
  
  testCompile 'org.apache.logging.log4j:log4j-api:2.7'
  testCompile 'org.apache.logging.log4j:log4j-core:2.7'
  testCompile group:'org.sensorhub', name:'sensorhub-core', version:oshCoreVersion, configuration: 'testArtifacts'
}

// only run tests using mock DB automatically
// exclude tests requiring connection to the database; these have to be run manually
test {
  exclude '**/integration/*.class'
}

// add info to OSGi manifest
osgi {
  manifest {
    attributes('Bundle-Vendor': 'Sensia Software LLC')
    attributes('Bundle-Activator': 'org.sensorhub.impl.persistence.es.Activator')
    attributes('-exportcontents': 'org.sensorhub.impl.persistence.es')
    attributes('Import-Package': '''
      !android.*,!com.carrotsearch.*, !com.fasterxml.*, !com.google.*, !com.jcraft.*, !com.ning.*, !com.sun.*,
      !java.*, !javassist.*, !javax.*, !lzma.*, !net.jpountz.*, !org.apache.*, !org.bouncycastle.*, !org.eclipse.jetty.*, 
      !org.ietf.*, !org.jboss.*, !org.joda.*, !org.noggit.*, !org.osgi.service.log.*, !org.w3c.dom.*, !org.xml.*, !sun.*, 
      
      *''')
  }
}

// add info to maven pom
ext.pom >>= {
  developers {
    developer {
      id 'mdhsl'
      name 'Mathieu Dhainaut'
    }
  }
}

