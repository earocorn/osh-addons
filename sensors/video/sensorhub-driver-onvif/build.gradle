description = 'ONVIF Video Camera'
ext.details = 'Driver for IP video cameras using the ONFIV standard'
version = '0.0.1'

dependencies {
  implementation 'org.sensorhub:sensorhub-core:' + oshCoreVersion
  implementation project(':sensorhub-driver-rtpcam')
  embeddedImpl 'org.apache.cxf.services.ws-discovery:cxf-services-ws-discovery-api:3.1.11'
  embeddedImpl 'org.apache.cxf.services.ws-discovery:cxf-services-ws-discovery-service:3.1.11'
  embeddedImpl 'org.apache.cxf:cxf-rt-transports-http-jetty:3.1.11'
  embeddedImpl 'org.apache.cxf:cxf-rt-bindings-soap:3.1.11'
  embeddedImpl 'javax.xml.soap:javax.xml.soap-api:1.3.8'
  embeddedImpl('net.sf.jipcam:jipcam:0.9.1') {
    exclude group: 'javax.servlet', module: 'servlet-api'
    exclude group: 'commons-cli', module: 'commons-cli'
    exclude group: 'commons-httpclient', module: 'commons-httpclient'
  }

  embeddedImpl fileTree(dir: 'lib', include: ['*.jar'])
  testCompile project(path: ':sensorhub-driver-videocam', configuration: 'testArtifacts')
}

// exclude tests requiring connection to the sensor
// these have to be run manually
test {
  exclude '**/TestOnvifCameraDriver.class'
}

// add info to OSGi manifest
osgi {
  manifest {
    attributes('Bundle-Vendor': 'Botts Innovative Research, Inc.')
    attributes('Bundle-Activator': 'org.sensorhub.impl.sensor.onvif.Activator')
    attributes('-fixupmessages': 'The default package \'.\' is not permitted by the Import-Package syntax.; is:=ignore')
    attributes('Import-Package': '''
      !com.ibm.*,!com.ms.*,!com.sun.*,!java.*,!javax.activation.*,!javax.annotation.*,!javax.crypto.*,!javax.imageio.*,
      !javax.jms.*,!javax.jws.*,!javax.lang.*,!javax.mail.*,!javax.management.*,!javax.naming.*,!javax.net.*,
      !javax.security.*,!javax.sound.*, !javax.swing.*, !javax.tools.*, !javax.validation.*, !javax.xml.*, !net.sf.cglib.*,
      !netscape.*, !org.apache.*, !org.codehaus.*, !org.ietf.*, !org.jvnet.*, !org.osgi.service.*, !org.relaxng.*,
      !org.springframework.*, !org.w3c.*, !org.xml.*, !sun.*, 
      *''')
  }
}

// add info to maven pom
ext.pom >>= {
  developers {
    developer {
      id 'theastrowolfe'
      name 'Joshua Wolfe'
      organization 'Botts Innovative Research, Inc.'
      organizationUrl 'http://www.botts-inc.com' 
    }
  }
}
